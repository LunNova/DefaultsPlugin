plugins {
	id "com.gradle.plugin-publish" version "0.9.2"
}

apply plugin: 'maven'

defaultTasks 'build'

group="org.minimallycorrect.gradle"
version = "0.1-SNAPSHOT"

repositories {
	jcenter()
	maven { url = "https://repo.nallar.me/" }
	maven { url = "https://plugins.gradle.org/m2/" }
	maven { url = "https://files.minecraftforge.net/maven" }
}

configurations.all { resolutionStrategy.cacheChangingModulesFor 30, 'seconds' }

dependencies {
	compileOnly gradleApi()
	compileOnly "org.projectlombok:lombok:1.16.16"
	compileOnly "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
	//compileOnly "gradle.plugin.com.github.spotbugs:gradlePlugin:1.3"
	compile "com.diffplug.spotless:spotless-plugin-gradle:3.5.2"
	compile "org.shipkit:shipkit:0.9.76"
}

tasks.withType(JavaCompile) {
	sourceCompatibility = 8
	targetCompatibility = 8
	options.with {
		deprecation = true
		encoding = 'UTF-8'
		compilerArgs << "-Xlint:all" << "-Xlint:-path" << "-Xlint:-processing"
	}
}

ext.buildNumber = System.getenv("BUILD_NUMBER") ?: 0
ext.ciSystem = System.getenv("JENKINS_URL") ? 'Jenkins' : 'unknown'
ext.commit = System.getenv("GIT_COMMIT") ?: 'unknown'

jar.manifest.mainAttributes (
	"Built-By": System.properties['user.name'],
	"Created-By": System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
	"Implementation-Title": name,
	"Implementation-Version": version + "+" + ciSystem + "-b" + buildNumber + ".git-" + commit,
)

jar {
	baseName = project.name
}

artifacts {
	archives jar
}

pluginBundle {
	website = 'http://www.gradle.org/'
	vcsUrl = 'https://github.com/minimallycorrect/ConventionPlugin'
	description = 'Sets up '
	tags = ['minecraft']
	plugins << {
		ConventionPlugin {
			id = 'org.minimallycorrect.gradle.ConventionPlugin'
			displayName = 'LibLoader Gradle Plugin'
		}
	}
}
